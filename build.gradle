plugins {
    id 'dev.architectury.loom' version '1.9-SNAPSHOT'
    id 'maven-publish'
}

group = project.maven_group
version = project.mod_version

base {
    archivesName = project.archives_name
}

loom {
    silentMojangMappingsLicense()

    forge {
        mixinConfig 'gregwolvescore.mixins.json'
    }
}

repositories {
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        name = "GTCEu Maven"
        url = "https://maven.gtceu.com"
        content {
            includeGroup 'com.gregtechceu.gtceu'
        }
    }
}

dependencies {
    minecraft "net.minecraft:minecraft:$project.minecraft_version"
    mappings loom.officialMojangMappings()
    forge "net.minecraftforge:forge:$project.forge_version"

    modImplementation "maven.modrinth:architectury-api:${architectury_version}" // Architectury isn't uploaded Modrinth's maven under MC versions as far as I can tell so we'll just use the explicit one for now. TODO: Figure out a better way of importing it.
    modImplementation "maven.modrinth:lithostitched:${lithostitched_version}-forge-1.20" // Can't use minecraft_version here because for some reason LS has its 1.20.1 jar labeled for 1.20
    modImplementation "com.gregtechceu.gtceu:gtceu-${minecraft_version}:${gtceu_version}"
    modImplementation "maven.modrinth:farmers-delight:${minecraft_version}-${farmers_delight_version}"
    modImplementation "maven.modrinth:natures-spirit:${natures_spirit_version}-${minecraft_version}"
    modImplementation "maven.modrinth:natures-delight:${natures_delight_version}-${minecraft_version}"
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/mods.toml') {
        expand version: inputs.properties.version
    }
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

// Configure Maven publishing.
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
